# Lesson 03

Starting with this lesson, I'm putting the source code for the shaders into `script` tags. Again,
the shader source just needs to be a JavaScript string, and you can create it however you want, but
`script` tags are a very common way of doing so, since there's not really a great way of writing
multi-line strings in JavaScript. As long as you set the `type` attribute to something other than
`"text/javascript"`, the script tag will be skipped over, so you can put whatever you want within
it. Often the `type` is set to `"x-shader/x-vertex"` for vertex shader source, and
`"x-shader/x-fragrment"` for fragment shader source, but these values don't have any special meaning
as far as JavaScript or WebGL is concerned.

I'll be using a feature that works in many browsers: if a `script` tag has `id="vsource"`, you can
refer to it as `vsource` within JavaScript, so `vsource.text` refers to the text of the `script`
tag. Technically this feature is not in the JavaScript spec, and it's not guaranteed to work in all
browsers. Really you should be including something like this:

	var vsource = document.getElementById("vsource")

Running the program
===================

A WebGL program is run whenever you issue a drawing command. Despite the huge range that WebGL is
capable of, there are only two drawing commands, and they're quite similar: `gl.drawArrays` and
`gl.drawElements`. The only drawing command we're going to use for a while is:

	gl.drawArrays(gl.POINTS, 0, 1)

This tells WebGL to draw a single point. Where it appears, how big it appears, and what color or
colors it is all depend on the shader source code.

First the vertex shader runs, in parallel, for each vertex. (Since we're only drawing one point,
there's nothing to parallelize here, but it will come up later.) The vertex shader assigns a value
to the global variable `gl_Position`, that determines where on the screen the point will appear. It
may also assign a value to `gl_PointSize`, which is the size of the point in pixels, and which
defaults to `1.0`.

Next the fragment shader runs, in parallel, for each fragment. As far as we're concerned, a fragment
is the same as a pixel. For every pixel that falls within the point, the fragment shader will
calculate and assign a value to `gl_FragColor`. This is the color that actually appears in the
viewport. If you draw a single point that's 10x10 pixels, the fragment shader will run 100 times in
parallel, and each run may produce a different color. This means you can have different colors for
different pixels within the same point, which we'll get to in Lesson 05.

Shader uniforms
===============

Here's the vertex shader source for this lesson:

	uniform mediump float size;
	uniform highp vec2 pos;
	void main() {
		gl_Position = vec4(pos, 0.0, 1.0);
		gl_PointSize = size;
	}

The basic syntax is pretty similar to C, which is itself similar to JavaScript in a lot of ways.
Lines need to end in a semicolon, and there needs to be a `main` function. Variable types are
strictly enforced. Unlike JavaScript, floating-point numbers and integers are completely different
types, and there's no automatic conversion between them. So you can't use `0` for a function
expecting a float. You need to use `float(0)` or `0.0`.

Here we see the first kind of shader variables: uniforms. Other kinds we'll encounter are constants,
attributes, and varyings. These kinds relate to where the variable value comes from, and whether
it's the same between different (parallel) shader invocations. The value of uniform variables can be
set from JavaScript, but only between drawing commands. For the duration of a single drawing
command, a uniform variable has a single value.

The two uniforms declared here are `size`, which is a medium-precision floating point number, and
`pos`, which is a 2-element vector of high-precision floats. We'll talk more about types in the next
lesson. The uniforms get set from JavaScript, but before going into that, let's try to understand
this shader source.

The assignment to `gl_PointSize` here is pretty straightforward. It just gets its value from the
uniform `size`. If `size` is `10.0`, then the size of the point drawn to the viewport will be 10
pixels, centered on the position determined by `gl_Position`.

Vector assigment
================

The assignment to `gl_Position` raises a lot of questions. First off, what's up with the syntax?
What does `vec4(pos, 0.0, 1.0)` mean? GLSL has really handy syntax for working with vectors, since
vectors are so commonly used. In this case `vec4` is a constructor for a 4-element vector, taking
a 2-element vector and two floats. Anything that would give you a total of four elements would work
here. Overall, the assignment line is equivalent to these four assigments:

	gl_Position[0] = pos[0];
	gl_Position[1] = pos[1];
	gl_Position[2] = 0.0;
	gl_Position[3] = 1.0;

You can also refer to vector elements in terms of their components (x, y, z, and w), like this:

	gl_Position.x = pos.x;

And you can multiple subscripts to refer to subvectors:

	gl_Position.xy = pos.xy;
	gl_Position.zw = vec2(0.0, 1.0);

This is a bit strange at first, but it's really nice once you get used to it.

Position vectors
================

The next question is, why does a position have 4 elements? Why not 3 or even 2? And what do the `z`
and `w` coordinates mean?

The `z` coordinate does pretty much what you expect - it determines the effective "depth" of the
object you're drawing. Using it effectively is a little tricky, though, and we'll cover that in the
next lesson. Setting it to `0.0` for now is good enough.

The `w` coordinate is trickier. If vector math confuses you, just set it to `1.0` always and don't
worry about it. If you're interested in this sort of thing, though, read up on homogeneous
coordinates. Roughly speaking, the homogeneous coordinate (x, y, z, w) is equivalent to the
Euclidean coordinate (x/w, y/w, z/w), except that w can also be 0. Some calculations are easier in
homogeneous coordinates, which is why WebGL uses them. In particular, affine transformations on
homogeneous coordinates can be represented by matrices. I'm not going to be covering any advanced
usages of them, however, and I'll always be setting `w` coordinates to `1.0`.

Clip space
==========

The final question is, what are the units of `gl_Position`? `gl_Position` uses clip space
coordinates. Clip space runs from -1 to 1 in x, y, and z. So as long as all three of these
coordinates are within the range [-1, 1], the object will appear in the viewport.

In order to be displayed, the clip space coordinates are mapped to viewport coordinates, so that
`gl_Position.x = -1.0` corresponds to the left edge of the viewport, and `gl_Position.x = 1.0` is
the right edge. Coordinates run bottom to top, so `gl_Position.y = -1.0` is the bottom of the
viewport, and `gl_Position.y = 1.0` is the top. `gl_Position.xy = vec2(0.0, 0.0)` is at the center
of the viewport. In general, clip space coordinates are not square.

Generally you won't work in clip space within your game. You'll use some other coordinate system
that makes sense for you, and then map it to clip space within the vertex shader.

The fragment shader
===================

This looks pretty similar to the vertex shader. A uniform (a vector of 3 medium-precision floats in
this case) is used to assign a value to the global `gl_FragColor`:

	uniform mediump vec3 color;
	void main() {
		gl_FragColor = vec4(color, 1.0);
	}

Uniforms are shared between vertex and fragment shaders. A uniform doesn't need to be declared in
both shaders, but if it is, it needs to have the same type in both shaders, and it will have the
same value when each of them is run.

Like `gl_Position` and `gl_PointSize`, fragment shaders have two global values that they can set:
`gl_FragColor` and `gl_FragData`. For the most part, you'll only worry about `gl_FragColor`. It's a
vector of 4 floats between `0.0` and `1.0`, which are the red, green, blue, and alpha values of the
pixel color produced in this fragment. Like x/y/z/w, you can also use r/g/b/a to index a vector in
GLSL. This is equivalent to the above shader code:

	gl_FragColor.rgb = color;
	gl_FragColor.a = 1.0;

Position vectors and color vectors are not different types. You can just as easily say
`gl_FragColor.xyz` as `gl_FragColor.rgb` and it means the same thing. You can even say
`gl_FragColor.xgz`. It's just a matter of clarity: prefer `xyzw` if you're using a vector as a
position, and `rgba` if you're using it as a color.

Setting shader uniforms
=======================

So now we know that this WebGL program has three uniforms, `size`, `pos`, and `color`, and their
values need to be set before the drawing command is issued. Let's go back to JavaScript now and see
how to set a uniform's value from the `gl` context and the `prog` program object. It would be nice
if you could just say:

	prog.pos = [0.5, 0]

But here's what you actually have to do:

	var pos = gl.getUniformLocation(prog, "pos")
	gl.uniform2f(pos, 0.5, 0)

`pos` in this case is a WebGL uniform location object, meaning it refers to the location in the
program's memory space where the uniform variable `pos` is stored, and in order to set the value of
`pos` you'll need to write to this location in memory. Like shader objects and program objects,
uniform location objects don't have any methods. Instead you pass them to one of the uniform setter
methods of `gl`. The name of the setter method depends on the type of the uniform you're setting.
`gl.uniform2f` is the setter for `vec2`, a vector of 2 floats. The setter for `int` would be
`gl.uniform1i`. There's always a number 1-4, followed by either `i` or `f`, depending on whether
it's an integer (or boolean or sampler) or a float.

There are also "vector" setters, which take Arrays instead of separate arguments. These also have a
`v` at the end of their names. These are equivalent:

	gl.uniform2f(pos, 0.5, 0)
	gl.uniform2fv(pos, [0.5, 0])

Finally, WebGL has 2x2, 3x3, and 4x4 float matrix types. The uniform setters for these are only in
vector form, and they take an extra argument (transpose), which must always be set to `false`.

	// Setter for a 3x3 matrix uniform
	// rot is the uniform location for a 3x3 matrix uniform
	gl.uniformMatrix3fv(rot, false, [1, 2, 3, 4, 5, 6, 7, 8, 9])

Once you're comfortable with all this, I recommend using a wrapper function. The `util.js` module
has one that works like this:

	util.setUniforms(gl, {pos: [0.5, 0], size: 10})

It's not the most efficient, because it uses `gl` getters to look up the type and location of the
uniforms every time you call it. But it's fine for our purposes. See `03a.html` for a version of
lesson 03 rewritten using `util.setUniforms`.

Missing uniforms
================

One warning about something that always gets me. Shaders are highly optimized when they're compiled.
If a uniform is unused by the shader, it may be optimized away. For instance, say you're debugging
and you temporarily add a line to the end of the vertex shader in this lesson:

	uniform mediump float size;
	uniform highp vec2 pos;
	void main() {
		gl_Position = vec4(pos, 0.0, 1.0);
		gl_PointSize = size;
		gl_PointSize = 10.0;  // new line added for debugging
	}

Now all the points will be 10px in size, regardless of what the `size` uniform is set to, but will
anything else change? Possibly. The compiler may
realize that the value of `size` does not affect the output of the program, and optimize it away.
This means when you call `gl.getUniformLocation(prog, "size")` in JavaScript, you'll get `null`,
because no such uniform exists in the program. Depending on what you do with it, this may cause
problems in your JavaScript program. If you don't understand why your uniforms are apparently
missing from your WebGL program, it could be because they're not actually used.

Running the program
===================

Once you've set all the uniforms, it's time to actually draw something by calling the draw method:

	gl.drawArrays(gl.POINTS, 0, 1)

In this lesson, the draw method is called three times, once for each point. The uniforms are set to
different values before each call using the `gl.uniform*` setters.

At least in Chrome, you're going to get a warning when you do run this program:

	PERFORMANCE WARNING: Attribute 0 is disabled. This has signficant performance penalty 

Just ignore it. We'll learn to enable attributes in lesson 10. We're leaving them disabled now just
for learning purposes, and it's not hurting anything.

Getters used in this lesson
===========================

	// Number: number of uniforms in the program
	gl.getProgramParameter(prog, gl.ACTIVE_UNIFORMS)
	// WebGLActiveInfo object: info on the corresponding uniform
	// Has attributes name (string), type (gl enum), and size (1 for non-vectors)
	gl.getActiveUniform(prog, n)
	// WebGLUniformLocation object
	gl.getUniformLocation(prog, "uniform_name")
	// Value of the uniform at the given location
	gl.getUniform(prog, uniform_location)

Exercises
=========

1. Change the green point to blue.
1. Add another point in the lower left, 50px in size and colored white.
1. Change the vertex shader so that x and y coordinates of `gl_Position` are swapped. The green and
yellow points should then appear swapped in position.
1. Change the vertex shader so that all the points are shifted down by a small amount.
1. Change the fragment shader so that the blue component is always set to 0.5 regardless of the
`color` uniform.
1. Replace the `pos` uniform with two uniform floats, `posx` and `posy`, that can be set separately.
Update the uniform setters so the viewport appears the same as it did before.
1. Combine the `pos` and `size` uniforms into a single `vec3` uniform `posandsize`. Use
`posandsize.xy` to set the position and `posandsize.z` to set the point size. Update the uniform
setters so the viewport appears the same as it did before.

